import groovy.json.JsonOutput
import groovy.json.JsonSlurper

plugins {
    id 'java-library'
    id 'fabric-loom' version '1.2-SNAPSHOT'
    id 'maven-publish'
}

version = project.omnimancy_version
group = project.omnimancy_group

base {
    archivesName = "${project.omnimancy_base_name}-spring"
}

repositories {
}

configurations {
    depInclude
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "${project.omnimancy_base_modid}-spring" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }

}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modApi "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modApi depInclude("org.springframework.boot:spring-boot-starter:3.1.0") {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
    }
}

remapJar {
    from(configurations.depInclude.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'META-INF', 'META-INF/**'
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    archiveClassifier.set("dev")

    from(rootProject.file("icon.png")) {
        // This is incorrect, I am lazy to fix this right now.
        rename { "META-INF/assets/omnimancy-spring/${it}" }
    }
    from(rootProject.file("LICENSE")) {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}